# Add source and header files of engine here
FILE(GLOB_RECURSE headers "include/*.h")
FILE(GLOB_RECURSE sources "src/*.cpp")

# Create static library for Engine
add_library(${SPACE_STU_ENGINE_TARGET_NAME} STATIC ${sources} ${headers})
source_group(TREE ${CMAKE_CURRENT_SOURCE_DIR}  FILES ${sources} ${headers})

target_include_directories(${SPACE_STU_ENGINE_TARGET_NAME} PUBLIC 
    ${CMAKE_CURRENT_SOURCE_DIR}/include
)

include(FetchContent)

set(SFML_LIB_NAME SFML)
FetchContent_Declare(
    ${SFML_LIB_NAME}
    GIT_REPOSITORY https://github.com/SFML/SFML
    GIT_TAG 2.6.1
    GIT_SHALLOW FALSE
)
FetchContent_MakeAvailable(${SFML_LIB_NAME})
target_link_libraries(${SPACE_STU_ENGINE_TARGET_NAME} PUBLIC sfml-graphics)
target_link_libraries(${SPACE_STU_ENGINE_TARGET_NAME} PUBLIC sfml-window)
target_link_libraries(${SPACE_STU_ENGINE_TARGET_NAME} PUBLIC sfml-system)
target_link_libraries(${SPACE_STU_ENGINE_TARGET_NAME} PUBLIC sfml-main)
target_link_libraries(${SPACE_STU_ENGINE_TARGET_NAME} PUBLIC sfml-audio)
target_link_libraries(${SPACE_STU_ENGINE_TARGET_NAME} PUBLIC sfml-network)


set(BOX2D_LIB_NAME Box2D)
FetchContent_Declare(
    ${BOX2D_LIB_NAME}
    GIT_REPOSITORY https://github.com/erincatto/box2d
    GIT_TAG v2.4.1
    GIT_SHALLOW FALSE
)
FetchContent_MakeAvailable(${BOX2D_LIB_NAME})
target_link_libraries(${SPACE_STU_ENGINE_TARGET_NAME} PUBLIC box2d)

function(CopyLibToTarget LIB_NAME TARGET_NAME)
    add_custom_command(
        TARGET ${TARGET_NAME}
        POST_BUILD
        COMMAND
        ${CMAKE_COMMAND} -E copy_if_different
        $<TARGET_FILE:${LIB_NAME}>
        $<TARGET_FILE_DIR:${TARGET_NAME}>
    )
endfunction()

CopyLibToTarget(sfml-graphics ${SPACE_STU_ENGINE_TARGET_NAME})
CopyLibToTarget(sfml-window ${SPACE_STU_ENGINE_TARGET_NAME})
CopyLibToTarget(sfml-system ${SPACE_STU_ENGINE_TARGET_NAME})
CopyLibToTarget(sfml-audio ${SPACE_STU_ENGINE_TARGET_NAME})
CopyLibToTarget(box2d ${SPACE_STU_ENGINE_TARGET_NAME})